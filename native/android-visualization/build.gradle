plugins {
    id "com.android.library" version "8.1.0"
    id "org.jetbrains.kotlin.android" version "1.8.22"
    id "maven-publish"
}

group = "com.soundwave"
version = "0.0.2-native-SNAPSHOT"

android {
    namespace "com.soundwave.visualization.core"
    compileSdk 34

    defaultConfig {
        minSdk 21
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    buildFeatures {
        buildConfig = false
    }
}

// 使用 AndroidX
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-Xjvm-default=all"]
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.3.0"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.soundwave"
                artifactId = "visualization-core"
                version = "0.0.2-native-SNAPSHOT"
                pom {
                    name = "SoundWave Visualization Core (Native)"
                    description = "Native-only visualization core (PCM/FFT/export) without Flutter dependency."
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "local"
                url = uri(findProperty("soundwaveRepoDir") ?: "${buildDir}/m2-local")
            }
        }
    }
}
